# ~/dotfiles/shell/zshrc_linux
# -----------------------------------------------------------------------------
# 0) Bail if not interactive
# -----------------------------------------------------------------------------
[[ $- != *i* ]] && return

# -----------------------------------------------------------------------------
# 1) Shell extras
# -----------------------------------------------------------------------------
source "$HOME/dotfiles/shell/git_pull_nudge.zsh" # remind on stale repos
source "$HOME/dotfiles/shell/weekly_update_check.zsh" # weekly update reminders

# -----------------------------------------------------------------------------
# 2) Prezto or fallback prompt
# -----------------------------------------------------------------------------
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
    source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
else
    source "$HOME/dotfiles/shell/zsh_prompt_fallback.zsh"
fi

# -----------------------------------------------------------------------------
# 3) FZF: fast fuzzy finder
# -----------------------------------------------------------------------------
if command -v fd &>/dev/null; then
    export FZF_DEFAULT_COMMAND='fd --type f --strip-cwd-prefix --hidden --follow --exclude .git'
elif command -v rg &>/dev/null; then
    export FZF_DEFAULT_COMMAND='rg --files --hidden --follow --glob "!.git/*"'
else
    export FZF_DEFAULT_COMMAND='find . -path "*/\.*" -prune -o -type f -print 2>/dev/null | cut -b3-'
fi
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND" # ctrl-T lookup
export FZF_ALT_C_COMMAND='fd --type d --strip-cwd-prefix --hidden --follow --exclude .git'
export FZF_DEFAULT_OPTS='--height 60% --layout=reverse --border --preview "head -100 {}" --preview-window=right:50%:wrap'

for p in /usr/share/fzf/key-bindings.zsh /usr/share/doc/fzf/examples/key-bindings.zsh "$HOME/.fzf/shell/key-bindings.zsh"; do
    [[ -f $p ]] && source $p && break
done
for p in /usr/share/fzf/completion.zsh /usr/share/doc/fzf/examples/completion.zsh "$HOME/.fzf/shell/completion.zsh"; do
    [[ -f $p ]] && source $p && break
done

# -----------------------------------------------------------------------------
# 4) Aliases & enhancers
# -----------------------------------------------------------------------------
# “.” up shortcuts
source "$HOME/dotfiles/shell/alias_dots.zsh"

# tar with exclusion
source "$HOME/dotfiles/shell/alias_tar.zsh"

# distro package shortcuts
if [[ -f /etc/os-release ]]; then
    . /etc/os-release
    case "$ID" in
    fedora)
        alias update='sudo dnf update'
        alias install='sudo dnf install'
        alias search='dnf search'
        ;;
    ubuntu | debian)
        alias update='sudo apt update && sudo apt upgrade'
        alias install='sudo apt install'
        alias search='apt search'
        ;;
    arch | manjaro)
        alias update='sudo pacman -Syu'
        alias install='sudo pacman -S'
        alias search='pacman -Ss'
        ;;
    esac
fi

# Nicer versions of common commands
if command -v eza &>/dev/null; then
    source "$HOME/dotfiles/shell/alias_eza.zsh"
else
    alias l='ls -la'
fi

# pdu (parallel disk usage) - better du replacement
if command -v pdu &>/dev/null; then
    alias du='pdu --top-down --progress --silent-errors'
fi

# -----------------------------------------------------------------------------
# 5) Handy functions
# -----------------------------------------------------------------------------
ipv4() {
    curl -4fsSL https://ifconfig.me
}
ipv6() {
    if command -v ip &>/dev/null; then
        ip -6 addr show scope global | grep -oP '(?<=inet6 )[0-9A-Fa-f:]+'
    else
        ifconfig | awk '/inet6/&&!/fe80/ {print $2}'
    fi
}
command -v xdg-open &>/dev/null && alias open='xdg-open'

# -----------------------------------------------------------------------------
# 6) Prompt theme & system info
# -----------------------------------------------------------------------------
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
    autoload -Uz promptinit
    promptinit
    prompt paradox
    RPROMPT=''
fi

if command -v fastfetch &>/dev/null; then
    clear
    fastfetch --structure Break:Break:Break:Title:Break:OS:CPU:GPU:Disk:Memory:Uptime:Display:Kernel:Packages:Shell:DE:WM:Terminal
elif command -v neofetch &>/dev/null; then
    clear
    neofetch
fi
